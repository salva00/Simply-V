# Author: Stefano Mercogliano <stefano.mercogliano@unina.it>
# Author: Vincenzo Maisto <vincenzo.maisto2@unina.it>
# Author: Valerio Di Domenico <valer.didomenico@studenti.unina.it>
# Author: Salvatore Santoro <sal.santoro@studenti.unina.it>
# Description: Startup code and vector table definition for Simply-V

# Exit status
.equ SIMPLYV_OK   , 0
.equ SIMPLYV_ERROR, 1

# Simple flag to sync _timer_handler and clint_sleep_ticks()
.section .bss
.global _timer_handler_flag
_timer_handler_flag:
  .space 4

################
# Vector table #
################
.section .vector_table, "ax"
.option norvc;

  # According to RISC-V Specification, all entries are jumps to the specific handler.
  # Only the reset handler is defined in this file, while all other handlers points to
  # the default_handler (a loop)

  # Reset handler
  jal x0, _reset_handler      # Entry 0

  jal x0, _default_handler    # Entry 1
  jal x0, _default_handler    # Entry 2

  # SIE handler
  jal x0, _sw_handler         # Entry 3

  jal x0, _default_handler    # Entry 4
  jal x0, _default_handler    # Entry 5
  jal x0, _default_handler    # Entry 6

  # TIE handler
  jal x0, _timer_handler      # Entry 7

  jal x0, _default_handler    # Entry 8
  jal x0, _default_handler    # Entry 9
  jal x0, _default_handler    # Entry 10

  # EIE handler
  jal x0, _ext_handler        # Entry 11

  jal x0, _default_handler    # Entry 12
  jal x0, _default_handler    # Entry 13
  jal x0, _default_handler    # Entry 14
  jal x0, _default_handler    # Entry 15
  jal x0, _default_handler    # Entry 16
  jal x0, _default_handler    # Entry 17
  jal x0, _default_handler    # Entry 18
  jal x0, _default_handler    # Entry 19
  jal x0, _default_handler    # Entry 20
  jal x0, _default_handler    # Entry 21
  jal x0, _default_handler    # Entry 22
  jal x0, _default_handler    # Entry 23
  jal x0, _default_handler    # Entry 24
  jal x0, _default_handler    # Entry 25
  jal x0, _default_handler    # Entry 26
  jal x0, _default_handler    # Entry 27
  jal x0, _default_handler    # Entry 28
  jal x0, _default_handler    # Entry 29
  jal x0, _default_handler    # Entry 30
  jal x0, _default_handler    # Entry 31

# Keep a dedicated sections for handlers
.section .text.handlers

_reset_handler:
  .global _reset_handler

  #####################
  # Registers Cleanup #
  #####################

  mv ra, zero
  mv sp, zero
  mv gp, zero
  mv tp, zero
  mv t0, zero
  mv t1, zero
  mv t2, zero
  mv s0, zero
  mv s1, zero
  mv a0, zero
  mv a1, zero
  mv a2, zero
  mv a3, zero
  mv a4, zero
  mv a5, zero
  mv a6, zero
  mv a7, zero
  mv s2, zero
  mv s3, zero
  mv s4, zero
  mv s5, zero
  mv s6, zero
  mv s7, zero
  mv s8, zero
  mv s9, zero
  mv s10, zero
  mv s11, zero
  mv t3, zero
  mv t4, zero
  mv t5, zero
  mv t6, zero

  #####################
  # Enable Interrupts #
  #####################

  # ponytail: picorv32 has no standard mtvec/mstatus/mie CSRs (custom IRQ scheme)
  # and hangs on these csrw. Skip them when assembled for picorv32
  # (-Wa,--defsym,CORE_PICORV32=1). Other cores: unchanged. Interrupt apps are
  # not supported on picorv32 anyway (see rv_socket.sv note).
.ifndef CORE_PICORV32
  # Set mtvec to vectored mode
  la a0, _vector_table_start  # Load vector table base address
  li a1, 1                    # Set vectored mode bit
  or a1, a1, a0
  csrw mtvec, a1              # Commit on mtvec register

  # Enable global interrupts
  csrs mstatus, 0x8           # Enable MIE in mstatus

  # Disable all interrupt lines in mie register
  csrs mie, zero
.endif

  ########
  # Tail #
  ########

  # Initialize the stack pointer
  la   sp, _stack_start

  # Jump to start function
  j _start

#################
# Weak handlers #
#################

# RISC-V SW interrupt
.weak _sw_handler
_sw_handler:
  j _sw_handler

# RISC-V TIM interrupt
.weak _timer_handler
_timer_handler:
  # Clear MTIE (Machine Timer Interrupt Enable)
  li t0, 0x0080
  csrc mie, t0
  # Set _timer_handler_flag = 1
  # NOTE: this is just for demonstration, avoiding to
  #       use atomics for those cores that do not support
  #       the A extention
  li t1, 1
  la t2, _timer_handler_flag
  sw t1, 0(t2)
  # Return
  mret

# RISC-V EXT interrupt
.weak _ext_handler
_ext_handler:
  j _ext_handler

# Default handler
.weak _default_handler
_default_handler:
  j _default_handler

.section .text.start

_start:
  .global _start

  # jump to main program entry point (argc = argv = 0)
  mv a0, zero
  mv a1, zero
  jal ra, main

  # Check exit status in a0
  # if SIMPLYV_OK
  li t0, SIMPLYV_OK
  beq a0, t0, _exit_wfi_ok
  # if SIMPLYV_ERROR
  li t0, SIMPLYV_ERROR
  beq a0, t0, _exit_wfi_error

# Spin in place
_exit_spin:
  j _exit_spin

# Hold program execution
_exit_wfi_error:
  wfi

# Hold program execution
_exit_wfi_ok:
  wfi





